function strlen(s) {
  var length = 0;
  while *s {
    s++;
    length++;
  }
  return length;
}

function strcmp(a, b) {
  while 1 {
    if *a < *b {
      return -1;
    } else if *b < *a {
      return 1;
    } else if *a == 0 && *b == 0 {
      return 0;
    }
    a++;
    b++;
  }
}

function strcpy(destination, source) {
  while *source {
    *destination = *source;
    source++;
    destination++;
  }
  *destination = 0;
}

function memcpy(destination, source, length) {
  var end = source + length;
  while source < end {
    *destination = *source;
    source++;
    destination++;
  }
}

function memmove(destination, source, length) {
  if destination < source {
    return memcpy(destination, source, length);
  } else if destination > source {
    # Needs to be copied backwards.
    var start = destination;
    destination += length;
    source += length;
    while start < destination {
      destination--;
      source--;
      *destination = *source;
    }
  }
}

function memset(destination, value, length) {
  var end = destination + length;
  while destination < end {
    *destination = value;
    destination++;
  }
}
